<cfset filePath = replace('#cgi.script_name#','//','/') >
<cfif fileexists(application.webDirectory & filePath)>
	<!--- cache for 45 mins on production, otherwise no cache ---> 
	<cfif Application.productionServer EQ true>
		<cfset cacheInterval = CreateTimeSpan(0,0,90,0)> 
	<cfelse>
		<cfset cacheInterval = CreateTimeSpan(0,0,90,0)> 
	</cfif>
	<!--- for redesign, look up form permissions in a separate table than for current MCZbase --->
	<cfquery name="validRolesForPage" datasource="uam_god" cachedWithin="#cacheInterval#">
		select ROLE_NAME from cf_form_permissions_r
		where form_path = <cfqueryparam value="#filePath#" cfsqltype="CF_SQL_VARCHAR">
	</cfquery>
	<cfif validRolesForPage.recordcount is 0>
		<!--- Likely cause: A page has been created, but no entry for it has been added to cf_form_permissions  --->
		<cfthrow message="Page Exists, but there is no entry for it in cf_form_permissions.  (Query Cache time: #cacheInterval#)">
	<cfelseif valuelist(validRolesForPage.role_name) is not "public">
		<cfloop query="validRolesForPage">
			<cfif not listfindnocase(session.roles,role_name)>
				<cfset bad=true>
			</cfif>
		</cfloop>
	</cfif>
	<cfif isdefined("bad") and bad is true>
		<cfoutput>
			<div class="container">
				<div class="row">
				<cftry>
					<cfmail subject="Access Violation" to="#Application.technicalEmail#" from="Security@#Application.fromEmail#" type="html">
					IP address (#cgi.HTTP_X_Forwarded_For# - #remote_host#) tried to access
					#cgi.script_name#

					<br>This message was generated by /CustomTags/rolecheck.cfm.
				</cfmail>
				<cfcatch></cfcatch>
				</cftry>

				<!--- make sure they're really logged out --->
				<!--- TODO: Force logout --->
				<cfset currentPath=GetDirectoryFromPath(GetTemplatePath()) />
				<cfset r=replace(currentPath,application.webDirectory,"") />
				<cfheader statuscode="403" statustext="Forbidden">
				<cfscript>getPageContext().forward("/errors/forbidden.cfm?ref=#r#");</cfscript>
				<cfabort />
			</div>
				</div>
		</cfoutput>
	</cfif>
</cfif>
